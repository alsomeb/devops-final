plugins {
	id 'java'
	id 'org.springframework.boot' version '3.1.4'
	id 'io.spring.dependency-management' version '1.1.3'
	id 'jacoco' // LÃ¤gg till JaCoCo plugin
	id 'checkstyle' // Google Check Style
}

group = 'com.alsomeb'
version = '1.0.0'

java {
	sourceCompatibility = '17'
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.rest-assured:rest-assured:5.3.2' // Rest Assured Dependency
}

tasks.named('test') {
	useJUnitPlatform()
}

jacocoTestReport {
	dependsOn test // tests are run before generating the report
	reports {
		xml.required = true
	}
}

checkstyle {
	configFile = file("${project.rootDir}/config/checkstyle/google_checks.xml")
	toolVersion = '10.12.4'
	reportsDir = file("${project.buildDir}/checkstyle")
	showViolations = true // Denna rad visar felen i konsolen
}

tasks.withType(Checkstyle) {
	reports {
		xml.required = false
		html.required = true
	}
}

test {
	useJUnitPlatform {
		excludeTags 'integrationTest', 'systemTest'
	}
	finalizedBy jacocoTestReport // report is generated after tests run
}

tasks.register('integrationTest', Test) {
	useJUnitPlatform {
		includeTags 'integrationTest'
		excludeTags 'systemTest'
	}
}

tasks.register('systemTest', Test) {
	useJUnitPlatform {
		includeTags 'systemTest'
	}
}